#!/usr/bin/env zsh
#  _ __   __ _ _ __ __ _ _ __ ___  ___
# | '_ \ / _` | '__/ _` | '_ ` _ \/ __|
# | |_) | (_| | | | (_| | | | | | \__ \
# | .__/ \__,_|_|  \__,_|_| |_| |_|___/
# |_|
# Display shell parameters somewhat nicely
# h@h-lo.me 20190130 144346 -0800 PST 1548888226 d(-_- )b...
(( $+DEBUG  )) && setopt verbose xtrace

function maxsize () {
  local bigger=0
  for e in ${(P)argv[1]}
     [[ ${#e} -gt $bigger  ]] && bigger=${#e}
  echo $bigger
  unset bigger
}

local -a keys attrs
for k in ${(k)parameters}
  keys=($k $keys)

for j in ${(k)parameters}
  attrs=(${(Pt)j} $attrs)

largest=$(maxsize keys)
longest=$(maxsize attrs)
param_attr="%F{blue}%%-${longest}.${longest}s%f"
param_names="%F{green}%%${largest}.${largest}s%f"
param_values="%%s\n"
fmt="$param_names $param_attr $param_values"

for k in ${(ko)parameters} ; do
  print -f ${(%)fmt} "${k}" "${(Pt)k}" "${(VqP)k}"
done | less -C


(( $+DEBUG )) && unsetopt verbose xtrace
#  vim: set ft=zsh sw=2 tw=0 fdm=marker foldlevel=0 et :
